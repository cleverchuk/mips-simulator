plugins {
	id 'maven-publish'
	alias(libs.plugins.agp)
	alias(libs.plugins.spotless)
}

android {
	namespace = "com.cleverchuk.mips"
	compileSdk = 36

	defaultConfig {
		minSdk 24
		targetSdk 36

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		consumerProguardFiles "consumer-rules.pro"
		versionName = versions.name
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	buildToolsVersion '33.0.0'

	publishing {
		singleVariant('release') {
			withSourcesJar()
		}
	}

    packagingOptions {
        resources {
            excludes += ['META-INF/kotlinx_coroutines_core.version']
        }
    }
}


publishing {
	publications {
		release(MavenPublication) {
			groupId 'com.cleverchuk'
			artifactId 'mips-simulator'
			version versions.name
			pom {
				name = 'mips-simulator'
				description = 'Mips iDE\'s simulator and compiler backend'
				url = 'https://github.com/cleverchuk/mips-simulator'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://github.com/cleverchuk/mips-simulator/blob/main/LICENSE'
					}
				}
				developers {
					developer {
						id = 'cleverchuk'
						name = 'Chukwubuikem Ume-Ugwa'
						email = 'cleverchuk@gmail.com'
					}
				}
				scm {
					connection = 'scm:git:git@github.com:cleverchuk/mips-simulator.git'
					developerConnection = 'scm:git:ssh:git@github.com:cleverchuk/mips-simulator.git'
					url = 'https://github.com/cleverchuk/mips-simulator'
				}
			}
			afterEvaluate {
				from components.release
			}
		}
	}
}

spotless {
	java {
		target 'src/*/java/**/*.java'
		importOrder()
		removeUnusedImports()
        forbidWildcardImports()

		googleJavaFormat()
		formatAnnotations()
	}

	groovyGradle {
		greclipse()
	}
}

configurations.implementation {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

dependencies {
	implementation libs.javax.inject
	implementation libs.appcompat

	testImplementation libs.junit
	testImplementation libs.mockito.core
	androidTestImplementation libs.ext.junit
	androidTestImplementation libs.espresso.core
}